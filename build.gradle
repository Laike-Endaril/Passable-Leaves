buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply from: 'build-common.gradle'

sourceCompatibility = targetCompatibility = 1.8

group = project.package_base
archivesBaseName = "${project.name}-${project.mc_version}"
def coremodBaseName = "${project.name}Core-${project.mc_version}"
version = project.mod_version

reobf { coreJar { mappingType = 'SEARGE' } }

task coreJar(type: Jar) {
    from(sourceSets.main.output) { include "teamrtg/passableleaves/asm/**" }
    from(sourceSets.main.resources) {
        include "assets/logo.png"
        include "META-INF/passableleaves_at.cfg"
    }
    setBaseName coremodBaseName
    setVersion project.coremod_version
    manifest {
        attributes ([
            "FMLAT": "passableleaves_at.cfg",
            "FMLCorePlugin": "teamrtg.passableleaves.asm.PassableLeavesCore",
            "Maven-Artifact": "${project.group}:${coremodBaseName}:${project.coremod_version}"
        ])
    }
}

jar {
    exclude "teamrtg/passableleaves/asm/**"
    exclude "assets/passableleavescore/**"
    exclude "META-INF/passableleaves_at.cfg"
    from(coreJar.archivePath.absolutePath) {}
    manifest {
        attributes([
            "ContainedDeps": coreJar.archivePath.name,
            "Maven-Artifact": "${project.group}:${archivesBaseName}:${project.version}"
        ])
    }
    dependsOn reobfCoreJar
}

minecraft {
    version = project.mcf_version
    mappings = project.mcp_mappings
    runDir = "run"
    makeObfSourceJar = false
    replace "@MOD_VERSION@", project.version
    replaceIn "PassableLeaves.java"
    replace "@COREMOD_VERSION@", project.coremod_version
    replace "@ACCEPTABLE_MC_VER_RANGE@", project.acceptable_mc_ver_range
    replaceIn "PassableLeavesCore.java"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand ([
            "version"    : project.mod_version,
            "mcversion"  : project.mc_version
        ])
    }
    from(sourceSets.main.resources.srcDirs) {exclude "mcmod.info"}
}
